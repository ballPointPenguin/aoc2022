# 2022 Day 3

```elixir
Mix.install([:kino])
```

## Description

<details>
  <summary>
    Rucksack Reorganization: Part 1
  </summary>
  <p>One Elf has the important job of loading all of the <a href="https://en.wikipedia.org/wiki/Rucksack" target="_blank">rucksacks</a> with supplies for the <span title="Where there's jungle, there's hijinxs.">jungle</span> journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.</p>
  <p>Each rucksack has two large <em>compartments</em>. All items of a given type are meant to go into exactly one of the two compartments. The Elf that did the packing failed to follow this rule for exactly one item type per rucksack.</p>
  <p>The Elves have made a list of all of the items currently in each rucksack (your puzzle input), but they need your help finding the errors. Every item type is identified by a single lowercase or uppercase letter (that is, <code>a</code> and <code>A</code> refer to different types of items).</p>
  <p>The list of items for each rucksack is given as characters all on a single line. A given rucksack always has the same number of items in each of its two compartments, so the first half of the characters represent items in the first compartment, while the second half of the characters represent items in the second compartment.</p>
  <p>For example, suppose you have the following list of contents from six rucksacks:</p>
  <pre><code>
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
  </code></pre>
  <ul>
  <li>The first rucksack contains the items <code>vJrwpWtwJgWrhcsFMMfFFhFp</code>, which means its first compartment contains the items <code>vJrwpWtwJgWr</code>, while the second compartment contains the items <code>hcsFMMfFFhFp</code>. The only item type that appears in both compartments is lowercase <code><em>p</em></code>.</li>
  <li>The second rucksack's compartments contain <code>jqHRNqRjqzjGDLGL</code> and <code>rsFMfFZSrLrFZsSL</code>. The only item type that appears in both compartments is uppercase <code><em>L</em></code>.</li>
  <li>The third rucksack's compartments contain <code>PmmdzqPrV</code> and <code>vPwwTWBwg</code>; the only common item type is uppercase <code><em>P</em></code>.</li>
  <li>The fourth rucksack's compartments only share item type <code><em>v</em></code>.</li>
  <li>The fifth rucksack's compartments only share item type <code><em>t</em></code>.</li>
  <li>The sixth rucksack's compartments only share item type <code><em>s</em></code>.</li>
  </ul>
  <p>To help prioritize item rearrangement, every item type can be converted to a <em>priority</em>:</p>
  <ul>
  <li>Lowercase item types <code>a</code> through <code>z</code> have priorities 1 through 26.</li>
  <li>Uppercase item types <code>A</code> through <code>Z</code> have priorities 27 through 52.</li>
  </ul>
  <p>In the above example, the priority of the item type that appears in both compartments of each rucksack is 16 (<code>p</code>), 38 (<code>L</code>), 42 (<code>P</code>), 22 (<code>v</code>), 20 (<code>t</code>), and 19 (<code>s</code>); the sum of these is <code><em>157</em></code>.</p>
  <p>Find the item type that appears in both compartments of each rucksack. <em>What is the sum of the priorities of those item types?</em></p>
</details>

<!-- livebook:{"break_markdown":true} -->

<details>
  <summary>
    Rucksack Reorganization: Part 2
  </summary>
  <p>As you finish identifying the misplaced items, the Elves come to you with another issue.</p>
  <p>For safety, the Elves are divided into groups of three. Every Elf carries a badge that identifies their group. For efficiency, within each group of three Elves, the badge is the <em>only item type carried by all three Elves</em>. That is, if a group's badge is item type <code>B</code>, then all three Elves will have item type <code>B</code> somewhere in their rucksack, and at most two of the Elves will be carrying any other item type.</p>
  <p>The problem is that someone forgot to put this year's updated authenticity sticker on the badges. All of the badges need to be pulled out of the rucksacks so the new authenticity stickers can be attached.</p>
  <p>Additionally, nobody wrote down which item type corresponds to each group's badges. The only way to tell which item type is the right one is by finding the one item type that is <em>common between all three Elves</em> in each group.</p>
  <p>Every set of three lines in your list corresponds to a single group, but each group can have a different badge item type. So, in the above example, the first group's rucksacks are the first three lines:</p>
  <pre><code>
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
  </code></pre>
  <p>And the second group's rucksacks are the next three lines:</p>
  <pre><code>
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
  </code></pre>
  <p>In the first group, the only item type that appears in all three rucksacks is lowercase <code>r</code>; this must be their badges. In the second group, their badge item type must be <code>Z</code>.</p>
  <p>Priorities for these items must still be found to organize the sticker attachment efforts: here, they are 18 (<code>r</code>) for the first group and 52 (<code>Z</code>) for the second group. The sum of these is <code><em>70</em></code>.</p>
  <p>Find the item type that corresponds to the badges of each three-Elf group. <em>What is the sum of the priorities of those item types?</em></p>
</details>

## Input

```elixir
input = Kino.Input.textarea("Puzzle Input")
```

## Code

<h3>
Module<h3>
</h3>

```elixir
defmodule RuckSack do
  def priorities(input) do
    input
    |> process_input()
    |> Enum.map(
      &(&1
        |> compartments()
        |> get_diff()
        |> priority())
    )
    |> Enum.sum()
  end

  def badges(input) do
    input
    |> process_input()
    |> Enum.chunk_every(3)
    |> Enum.map(
      &(&1
        |> get_badge()
        |> priority())
    )
    |> Enum.sum()
  end

  defp compartments(list) do
    Enum.split(
      list,
      list
      |> Enum.count()
      |> Kernel.div(2)
    )
  end

  defp get_diff({comp1, comp2}) do
    MapSet.new(comp1)
    |> MapSet.intersection(MapSet.new(comp2))
    |> Enum.at(0)
  end

  defp get_badge([sack1, sack2, sack3]) do
    MapSet.new(sack1)
    |> MapSet.intersection(MapSet.new(sack2))
    |> MapSet.intersection(MapSet.new(sack3))
    |> Enum.at(0)
  end

  # A-Z in erlang is 65..90
  # a-z in erlang is 97..122
  defp priority(char) when char in 65..90, do: char - 38
  defp priority(char) when char in 97..122, do: char - 96

  defp process_input(input) do
    input
    |> String.split()
    |> Enum.map(&String.to_charlist/1)
  end
end
```

<h3>
Evaluate<h3>
</h3>

```elixir
part_1 =
  input
  |> Kino.Input.read()
  |> RuckSack.priorities()

part_2 =
  input
  |> Kino.Input.read()
  |> RuckSack.badges()

{part_1, part_2}
```

<h3>
Test<h3>
</h3>

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule RuckSackTest do
  use ExUnit.Case, async: true

  setup do
    # paste your example data after line 8
    input = ~s(
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
)

    {:ok, input: input}
  end

  test "priorities", %{input: input} do
    assert RuckSack.priorities(input) == 157
  end

  test "badges", %{input: input} do
    assert RuckSack.badges(input) == 70
  end
end

ExUnit.run()
```

```elixir
[head | tail] = [1]
{head, tail}
```
