# 2022 Day 2

```elixir
Mix.install([:kino])
```

## Description

<details>
  <summary>
    Rock Paper Scissors: Part 1
  </summary>
  <p>The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant <a href="https://en.wikipedia.org/wiki/Rock_paper_scissors" target="_blank">Rock Paper Scissors</a> tournament is already in progress.</p>
<p>Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.</p>
<p>Appreciative of your help yesterday, one Elf gives you an <em>encrypted strategy guide</em> (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: <code>A</code> for Rock, <code>B</code> for Paper, and <code>C</code> for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.</p>
<p>The second column, <span title="Why do you keep guessing?!">you reason</span>, must be what you should play in response: <code>X</code> for Rock, <code>Y</code> for Paper, and <code>Z</code> for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.</p>
<p>The winner of the whole tournament is the player with the highest score. Your <em>total score</em> is the sum of your scores for each round. The score for a single round is the score for the <em>shape you selected</em> (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the <em>outcome of the round</em> (0 if you lost, 3 if the round was a draw, and 6 if you won).</p>
<p>Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.</p>
<p>For example, suppose you were given the following strategy guide:</p>
<pre><code>A Y
B X
C Z
</code></pre>
<p>This strategy guide predicts and recommends the following:</p>
<ul>
<li>In the first round, your opponent will choose Rock (<code>A</code>), and you should choose Paper (<code>Y</code>). This ends in a win for you with a score of <em>8</em> (2 because you chose Paper + 6 because you won).</li>
<li>In the second round, your opponent will choose Paper (<code>B</code>), and you should choose Rock (<code>X</code>). This ends in a loss for you with a score of <em>1</em> (1 + 0).</li>
<li>The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = <em>6</em>.</li>
</ul>
<p>In this example, if you were to follow the strategy guide, you would get a total score of <code><em>15</em></code> (8 + 1 + 6).</p>
<p><em>What would your total score be if everything goes exactly according to your strategy guide?</em></p>
</details>
</details>

<!-- livebook:{"break_markdown":true} -->

<details>
  <summary>
    Rock Paper Scissors: Part 2
  </summary>

  <p>The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: <code>X</code> means you need to lose, <code>Y</code> means you need to end the round in a draw, and <code>Z</code> means you need to win. Good luck!"</p>
  <p>The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:</p>
  <ul>
  <li>In the first round, your opponent will choose Rock (<code>A</code>), and you need the round to end in a draw (<code>Y</code>), so you also choose Rock. This gives you a score of 1 + 3 = <em>4</em>.</li>
  <li>In the second round, your opponent will choose Paper (<code>B</code>), and you choose Rock so you lose (<code>X</code>) with a score of 1 + 0 = <em>1</em>.</li>
  <li>In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = <em>7</em>.</li>
  </ul>
  <p>Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of <code><em>12</em></code>.</p>
  <p>Following the Elf's instructions for the second column, <em>what would your total score be if everything goes exactly according to your strategy guide?</em></p>
</details>

## Input

```elixir
input = Kino.Input.textarea("Puzzle Input")
```

## Code

<h3>
Module<h3>
</h3>

```elixir
defmodule RockPaperScissors do
  def total_score(input) do
    input
    |> process_input()
    |> Enum.map(&score/1)
    |> Enum.sum()
  end

  def strategic_score(input) do
    input
    |> process_input()
    |> Enum.map(&strategic/1)
    |> Enum.sum()
  end

  # Convert Letters to Integers
  # Rock: 1, Paper: 2, Scissors: 3
  # or (Part 2) Loss: 1, Draw: 2, Win: 3
  defp numerize(char) when char in ["A", "X"], do: 1
  defp numerize(char) when char in ["B", "Y"], do: 2
  defp numerize(char) when char in ["C", "Z"], do: 3

  # draw
  defp score([num, num]), do: 3 + num
  # win
  defp score([3, 1]), do: 7
  defp score([1, 2]), do: 8
  defp score([2, 3]), do: 9
  # loss
  defp score([_, num]), do: num

  # loss
  defp strategic([1, 1]), do: 3
  defp strategic([them, 1]), do: them - 1
  # draw
  defp strategic([them, 2]), do: them + 3
  # win
  defp strategic([3, 3]), do: 7
  defp strategic([them, 3]), do: them + 7

  defp process_input(input) do
    input
    |> String.split()
    |> Enum.map(&numerize/1)
    |> Enum.chunk_every(2)
  end
end
```

<h3>
Evaluate<h3>
</h3>

```elixir
part_1 =
  input
  |> Kino.Input.read()
  |> RockPaperScissors.total_score()

part_2 =
  input
  |> Kino.Input.read()
  |> RockPaperScissors.strategic_score()

{part_1, part_2}
```

<h3>
Test<h3>
</h3>

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule RockPaperScissorsTest do
  use ExUnit.Case, async: true

  setup do
    input = ~s(
A Y
B X
C Z
)

    {:ok, input: input}
  end

  test "total_score", %{input: input} do
    assert RockPaperScissors.total_score(input) == 15
  end

  test "strategic_score", %{input: input} do
    assert RockPaperScissors.strategic_score(input) == 12
  end
end

ExUnit.run()
```
</details>
</details>
